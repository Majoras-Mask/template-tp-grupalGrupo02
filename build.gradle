        plugins {
    id 'de.aaschmid.cpd' version '0.4'
}

group 'ar.fiuba.tdd.tp-tp-grupal'
version '1.0'

allprojects {
    apply plugin: 'java'
    repositories {
        jcenter()
        maven {
            url "http://nexus-tecnicas7510.rhcloud.com/nexus/content/repositories/third-party"
        }
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion = '6.7'
        ignoreFailures = false
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    apply plugin: 'findbugs'
    findbugs {
        toolVersion = "3.0.0"
        sourceSets = [sourceSets.main]
        effort = "max"
    }

    apply plugin: 'pmd'
    pmd {
        toolVersion = '4.3'
        sourceSets = [sourceSets.main]
        ruleSets = ["basic", "braces", "strings", "unusedcode"]
    }

    apply plugin: 'cpd'
    cpd {
        toolVersion = '5.3.0'
    }

    cpdCheck {
        ignoreIdentifiers = true
        ignoreLiterals = true
        source = sourceSets.main.allJava
    }

    apply plugin: "jacoco"
    jacoco {
        toolVersion = "0.7.1.201405082137"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
            html.destination "${buildDir}/jacocoHtml"
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.+'
    }
}

project(':server') {
    plugins.withType(JavaBasePlugin) { // <- just required if 'java' plugin is applied within subproject
        check.dependsOn(cpdCheck)
    }
}